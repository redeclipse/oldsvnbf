// standard menu definitions

bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

newgui main [
	guibutton			"start.."		"showgui start"		"emblem"
	if (= $editing 1) [
		guibutton	"editing.."		"showgui editing"
	]
	guibutton			"options.."		"showgui options"
	guibutton			"help.."		"showgui help"
	guibutton			"about.."		"showgui about"
	guibutton			"quit"			"quit"				"exit"
]

newgui start [
	guibutton "server browser.."	"showgui servers" "server"
	guibar
	guibutton "local game" "connect localhost"
	guibutton "lanconnect"
	guibar
	guibutton "vote game/map.." "showgui gamemode"
	guibutton "disconnect"
]


newgui bots [
	guibutton "add one bot" [botadd 1]
	guibutton "add default bots" [botadd -1 -1]
	guibutton "remove one bot" [botdel 1]
	guibutton "remove all bots" [botdel 0]
	guibar
	guitext "bot ai rate"
	guislider botrate 1 100
	guibar
	guicheckbox "auto populate bots"	botauto
]

newgui help [
	guibutton "Multi-player help" "showgui mphelp"
	guitext "help on joining a multiplayer game"
	guibutton "Controls" "showgui conhelp"
	guitext "listing of default controls"
]

newgui mphelp [
	guitext "IMPORTANT: YOU MUST FORWARD UDP PORTS 28795 - 28796"
	guitext " "
	guitext "Joining an existing game:"
	guitext "open the console with `, and type connect server,"
	guitext "where server is the name of the server, or choose"
	guitext "update from master in the multiplayer menu and then"
	guitext "choose from a listed server."
	guitext " "
	guitext "Setting up a game:"
	guitext "Run server.bat instead of Blood_Frontier.bat"
]

newgui conhelp [
	guitext "DEFAULT CONTROLS:"
	guitext "W,A,S,D for movement and strafing"
	guitext "Spacebar to jump."
	guitext "Aim with your mouse."
	guitext "The left mouse button shoots."
	guitext "The right mouse button zooms the scope on the rifle"
]

newgui about [
	guitext "Blood Frontier (C) 2006-2008"
	guitext "  Anthony Cord and Quinton Reeves"
	guitext " "
	guitext "http://sourceforge.net/projects/bloodfrontier/"
	guitext " "
	guitext "Sauerbraten (Cube Engine 2) (C) 2001-2008"
	guitext "  Wouter van Oortmerssen and Lee Salzman"
]

maprotation = [
	n = (listlen $arg1)
	loop i $n [
	 curmap = (at $arg1 $i)
	 next = (mod (+ $i 1) $n)
	 nextmap = (at $arg1 $next)
	 (if (= $numargs 2) [concatword nextmap_ $arg2 $curmap] [concatword nextmap_ $curmap]) = $nextmap
	]
]

genmapitems = [
	n = (listlen $arg1)
	loop i $n [
	 curmap = (at $arg1 $i)
	 aliasname = (concatword bestscore_ $curmap)
	 title = (if (= (getalias $aliasname) 0) [ result $curmap ] [ concatword $curmap " (best score: " (getalias $aliasname) ")" ])
	 guibutton $title (concat map $curmap) "emblem"
	]
]

mainmaps = "eight usm10 usm09 usm01 usm02 usm03 usm06 redhot"
stfmaps = "usm10 usm09 usm02 usm03 usm06 redhot"
ctfmaps = "usm02 usm03 usm06 usm09"
spmaps = "uss01"

maprotation $mainmaps
maprotation $stfmaps stf_
maprotation $ctfmaps ctf_
maprotation $spmaps

alias showmapshot [
	guibar
	mapshot = (concatword "maps/" (at $guirollovername 0))
	guiimage $mapshot $guirolloveraction 4 1 "textures/nothumb"
]

newgui maps [ guilist [ guilist [ genmapitems $mainmaps ]; showmapshot ] ]
newgui stfmaps [ guilist [ guilist [ genmapitems $stfmaps ]; showmapshot ] ]
newgui ctfmaps [ guilist [ guilist [ genmapitems $ctfmaps ]; showmapshot ] ]
newgui spmaps [ guilist [ guilist [ genmapitems $spmaps ]; showmapshot ] ]

newgui servers [
	guistayopen [
		guilist [
			guibutton "update server list" "updatefrommaster"
			guibar
			guibutton "reset sorting" "serversortreset"
		]
	]
	guislider serverupdateinterval
	guiservers
]

newgui gamemode [
	guibutton "editing" "mode 1 0x0000; showgui maps"
	guibutton "deathmatch" "mode 3 0x0000; showgui maps"
	guibutton "secure the flag" "mode 4 0x0001; showgui stfmaps"
	guibutton "capture the flag" "mode 5 0x0001; showgui ctfmaps"

	guitab "dm"
	guibutton "normal" "mode 3 0x0000; showgui maps"
	guibutton "teamplay" "mode 3 0x0001; showgui maps"
	guibutton "instagib" "mode 3 0x0002; showgui maps"
	guibutton "team instagib" "mode 3 0x0003; showgui maps"
	guibutton "duel" "mode 3 0x0004; showgui maps"
	guibutton "team duel" "mode 3 0x0005; showgui maps"
	guibutton "instagib duel" "mode 3 0x0006; showgui maps"
	guibutton "team instagib duel" "mode 3 0x0007; showgui maps"
	guibutton "duel last man standing" "mode 3 0x0024; showgui maps"
	guibutton "team duel last man standing" "mode 3 0x0025; showgui maps"
	guibutton "instagib duel last man standing" "mode 3 0x0026; showgui maps"
	guibutton "team instagib duel last man standing" "mode 3 0x0027; showgui maps"
	guibutton "four team" "mode 3 0x0011; showgui maps"
	guibutton "four team instagib" "mode 3 0x0013; showgui maps"

	guitab "stf"
	guibutton "normal" "mode 4 0x0001; showgui stfmaps"
	guibutton "instagib" "mode 4 0x0003; showgui stfmaps"
	guibutton "progressive" "mode 4 0x0008; showgui stfmaps"
	guibutton "progressive instagib" "mode 4 0x000B; showgui stfmaps"
	guibutton "four team" "mode 4 0x0011; showgui stfmaps"
	guibutton "four team instagib" "mode 4 0x0013; showgui stfmaps"
	guibutton "four team progressive" "mode 4 0x0018; showgui stfmaps"
	guibutton "four team progressive instagib" "mode 4 0x001B; showgui stfmaps"

	guitab "ctf"
	guibutton "normal" "mode 5 0x0001; showgui ctfmaps"
	guibutton "instagib" "mode 5 0x0003; showgui ctfmaps"
	guibutton "four team" "mode 5 0x0011; showgui ctfmaps"
	guibutton "four team instagib" "mode 5 0x0013; showgui ctfmaps"
]

newmapsize = 12
savemap_name = temp

newgui editing [
    guibutton "materials.."                           "showgui materials"
    guibutton "toggle edit mode	(key F1)"              "edittoggle"
    guibutton "toggle heightmap	(key H)"              ""    
    guibutton "deselect		(key SPACE)"                "cancelsel"
    guibutton "passthrough	(hold ALT)"               "passthrough"
    guibutton "select		drag left mouse   "	          ""
    guibutton "select corners	drag middle mouse "     ""
    guibutton "extend selection	right mouse"          ""
    guibutton "reorient		right mouse"                ""
    guibutton "move selection	drag right mouse"       ""
    guibutton "copy		(key C)"			                  "copy"
    guibutton "paste		(key V)"			                "paste"
    
    guitab ents
    guilist [
      guilist [
        guibutton "drop entities.."              "showgui dropent"
        guibutton "mapmodels.."                  "showgui mapmodels"
        guibutton "light.."                      "resetlight;showgui newlight"
    
        guibutton "newent playerstart   "
        guibutton "newent weapon"
      ]
      guilist [
        guibutton "teleport.."                   "showgui newteleport"
        //guibutton "monster.."                    "showgui monsters"
        guibutton "trigger.."                    "showgui triggers"
      ]
    ]

    guitab keys    
    guibutton "flip / mirror cube	(key X)"            "flip"
    guibutton "undo		(key Z, U)"                     "undo"
    guibutton "redo		(key I)"                        "redo"
    guibutton "delete		(BACKSPACE)"                  "editdel"
    guibutton "texture palette     (F2)" "showtexgui"
    guibutton "edit entity gui	(F3)" "showentgui"
    guibutton "select all in box	(N)" "entselect insel"
    guibutton "select all matching	(M)" "selentfindall"
    guibutton "center view on ent	(COMMA + scroll)" "entautoview"
    guibutton "edit entity console	(PERIOD)" "selentedit"
    guibutton "toggle heightmap tex	(MMB in heightmapmode)" ""    
    guicheckbox "toggle outline		(key 7)"        outline
    guicheckbox "toggle wireframe		(key 8)"    wireframe

    guitab scroll
    guitext "push/pull selection		scroll wheel only"
    guitext "push the face of a cube	hold F + scroll wheel"
    guitext "change gridsize		hold G + scroll wheel"
    guitext "change textures		hold Y + scroll wheel"
    guitext "rotate         		hold R + scroll wheel"
    guitext "push corner with cursor	hold Q + scroll wheel"
    guitext "push/pull heightmap                hold LCTRL + scroll wheel"
    guitext "change heightmap brush	hold B + scroll wheel"

    guitab light
    guibutton "recompute lights (SLOW: 8xAA, alpha-masked model shadows)" "calclight 3"
    guibutton "recompute lights (SLOW: 8xAA, model shadows)" "calclight 2"
    guibutton "recompute lights (slow: 8xAA)"                "calclight 1"
    guibutton "recompute lights (default: 4xAA)"             "calclight 0"
    guibutton "recompute lights (quick: 2xAA)"               "calclight -1"
    guibutton "recompute lights (FAST: no AA, no shadows)"   "calclight -2"
    guibutton "patch lighting"                         "patchlight"

    guicheckbox "fullbright" fullbright

    guitext "lightprecision (default: 32)"
    guislider lightprecision

    guitext "lighterror (default: 8)"
    guislider lighterror

    //guibutton "lightlod high (6)" "lightlod 6"
    //guibutton "lightlod low  (2)" "lightlod 2"
    //guibutton "lightlod off  (0)" "lightlod 0"


    guitab mapop
    guitext   Savemap:
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton [load @savemap_name map]             "map $savemap_name"    
    guibutton "newmap (default size: 12)"          "newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "increase mapsize (2x)"              "mapenlarge"
    guitext   "max undo size (default: 5 MB)"
    guislider "undomegs" 1 10
    guibutton "optimize geometry"                  "remip"
    guibutton "set map title/author"               "saycommand [/mapmsg ]" 
]

newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "aiclip"                     "editmat aiclip"
    guibutton "death"                      "editmat death"

    guicheckbox "show material volumes"    showmat

    //guibutton "waterlod very high		(3)"  "waterlod 3"
    //guibutton "waterlod high		(2)"          "waterlod 2"
    //guibutton "waterlod default		(1)"  "waterlod 1"
    //guibutton "waterlod low		(0)"          "waterlod 0"

    //guibutton "watersubdiv very fine	(0)"          "watersubdiv 0"
    //guibutton "watersubdiv fine		(1)"  "watersubdiv 1"
    //guibutton "watersubdiv default		(2)"  "watersubdiv 2"
    //guibutton "watersubdiv coarse		(3)"  "watersubdiv 3"

    //guibutton "water waves on"                     "waterwaves 1"
    //guibutton "water waves off"                    "waterwaves 0"
]

setting_entediting = 1

newgui dropent [
    guitext "drop entities:"
    guiradio "to the floor"                 entdrop 1
    guiradio "on selection"                 entdrop 2
    guiradio "to the floor at selection"    entdrop 3
    guiradio "at current position"          entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"       entselsnap
    guicheckbox "entity selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

newgui triggers [
    guibutton [invisible]				[BTmm = -1; showgui trigger2]
    guibutton [a carrot]				[BTmm = 23; showgui trigger2]
    guibutton [switch #1]				[BTmm = 24; showgui trigger2]
    guibutton [switch #2]				[BTmm = 25; showgui trigger2]
    guibutton [door #1]					[BTmm = 26; showgui trigger2]
    guibutton [door #2]					[BTmm = 27; showgui trigger2]
    guibutton [door #3]					[BTmm = 28; showgui trigger2]
]

newgui trigger2 [
    guibutton [animation loop]						[BTtt = 0; showgui trigger3]
    guibutton [trigger once (popback)]				[BTtt = 1; showgui trigger3]
    guibutton [trigger once (popback & rumble)]		[BTtt = 2; showgui trigger3]
    guibutton [trigger once]							[BTtt = 3; showgui trigger3]
    guibutton [trigger once (rumble)]				[BTtt = 4; showgui trigger3]
    guibutton [trigger multiple (popback)]			[BTtt = 5; showgui trigger3]
    guibutton [trigger multiple (popback & rumble)]	[BTtt = 6; showgui trigger3]
    guibutton [trigger multiple]						[BTtt = 7; showgui trigger3]
    guibutton [trigger multiple (rumble)]			[BTtt = 8; showgui trigger3]
    guibutton [door (open once)]						[BTtt = 9; showgui trigger3]
    guibutton [door (auto close)]					[BTtt = 10; showgui trigger3]
    guibutton [door (locked)]						[BTtt = 11; showgui trigger3]
    guibutton [vanishing trigger]					[BTtt = 12; showgui trigger3]
    guibutton [vanishing trigger (rumble)]			[BTtt = 13; showgui trigger3]
    guibutton [End Of Level Trigger]					[BTtt = 29; showgui trigger3]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui trigger3 [
    loop i 9 [
	    tjt = (concat tag (concatword # $i))
	    tjc = (concat BTti "=" $i ";" "build_trigger")
	    guibutton $tjt $tjc 
    ]
]

resetlight = [
    lightcolour = 0
    lightrgb = "255 255 255"
    lightbright = 1
    lightradius = 128
]

lightcmd = [
    lightr = (at $lightrgb 0)
    lightg = (at $lightrgb 1)
    lightb = (at $lightrgb 2)
    if (= $lightbright 0) [
        if (strcmp $lightrgb "255 255 255") [
            lightr = 192; lightg = 192; lightb = 192
        ] [
            lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
         ]
    ]
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    guibutton "sunlight"    "newent light 0 255 200 100"
    guibutton (lightcmd)
    guibar
    guicheckbox "bright"    lightbright
    guitext "color:"
    guiradio "white"        lightcolour 0 [lightrgb = "255 255 255"]
    guiradio "blue"         lightcolour 1 [lightrgb = "192 192 255"]
    guiradio "red"          lightcolour 2 [lightrgb = "255 192 192"]
    guiradio "green"        lightcolour 3 [lightrgb = "192 255 192"]
    guiradio "yellow"       lightcolour 4 [lightrgb = "255 255 192"]
    guiradio "purple"       lightcolour 5 [lightrgb = "255 192 255"]
    guiradio "turquoise"    lightcolour 6 [lightrgb = "192 255 255"]
    guitext "radius:"
    guislider lightradius 0 512
]

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]


newgui options [
	guilist [
		guitext "name " player
		guifield newname 15 [name $newname] [newname = (getname)]
	]
	guilist [
		guitext "team " team
		guifield newteam 15 [team $newteam] [newteam = (getteam)]
	]

	guibar

	guicheckbox "auto connect on start" autoconnect
	guicheckbox "apply changes dialog" applydialog

	guibar
    guibutton "Restore Defaults" "exec defaults.cfg"

    guitab    "gfx"
    guitext "performance key: 0fast~, 2moderate~, 3slow and pretty~"
    guibar
    guilist [
        guicheckbox "2shaders	  "           shaders
	    if $shaders [
            guibar
            guiradio "0low detail" shaderdetail 1
            guibar
            guiradio "2high detail" shaderdetail 3
	    ]
    ]
    guilist [
        guitext "water	  "
        guibar
        guicheckbox "2refraction"  waterrefract
        guibar
        guicheckbox "3reflection"  waterreflect
        guibar
        guicheckbox "0caustics"    caustics
		if (&& (= $renderpath 0) $waterrefract) [
			guibar
        	guicheckbox "0depth fog"   refractfog      
		]
    ]
	if (> $renderpath 0) [
		guilist [
			guitext "waterfalls	  "
			guibar
			guicheckbox "2refraction" waterfallrefract
			guibar
			guicheckbox "0reflection" waterfallenv
		]
	]
    if (= $renderpath 0) [
        guicheckbox "0stencil shadows"   dynshadow 60 0
	] [
		guilist [
        	guicheckbox "3soft shadows"  shadowmap
			if $shadowmap [
                guibar
        	    guiradio "2medium quality" shadowmapsize 9 [blurshadowmap 1]
    	        guibar
	            guiradio "3high quality" shadowmapsize 10 [blurshadowmap 2]
			] [
				guibar
				guicheckbox "0stencil shadows" dynshadow 60 0
			]
    	]
    	if $glare [
        	glarepreset = 0
        	if (= $glarescale 1) [
            	if (= $blurglare 4) [glarepreset = 1]
            	if (= $blurglare 7) [glarepreset = 3]
        	]
        	if (= $glarescale 2) [
            	if (= $blurglare 3) [glarepreset = 2]
            	if (= $blurglare 7) [glarepreset = 4]
        	]
        	guilist [
            	guicheckbox "3glare	  "       glare
            	guibar
            	guiradio "2subtle" glarepreset 1 [blurglare 4; glarescale 1]
            	guibar
            	guiradio "2glowy" glarepreset 2 [blurglare 3; glarescale 2]
            	guibar
            	guiradio "3soft" glarepreset 3 [blurglare 7; glarescale 1]
            	guibar
            	guiradio "3intense" glarepreset 4 [blurglare 7; glarescale 2]
        	]
    	] [
        	guicheckbox "3glare"         glare
    	]
    ]
    guicheckbox "2grass"             grass
    if (> $renderpath 0) [
        guicheckbox "0dynamic lights"    maxdynlights 3 0
        guicheckbox "0soft explosions" depthfx
	]
    guicheckbox "0water/lava vertex animation" vertwater
    guicheckbox "0glass reflection"  glassenv
    guicheckbox "0decals" decals
    guicheckbox "0fix t-joints (world sparklies)" filltjoints
    guilist [
        guitext "textures    "
        guibar
        guiradio "0low quality" maxtexsize 256
        guibar
        guiradio "2high quality" maxtexsize 0
	]
    guilist [
        guitext "models	  "
        guibar
        guicheckbox "0lighting"    lightmodels
        guibar
        guicheckbox "0reflection"  envmapmodels
        guibar
        guicheckbox "0glow"        glowmodels
        if (> $renderpath 0) [
        guibar
            guicheckbox "0bumpmap" bumpmodels
        ]
    ]

    guitab    "display"
    guicheckbox "v-sync"            vsync 1 0
    guicheckbox "fullscreen"        fullscreen
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "color depth (default: 0)"
    guilistslider colorbits "0 8 16 24 32"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 8 16 24 32"
    guitext "stencil bits (default: 1)"
    guislider stencilbits
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"
	guilist [
    	guicheckbox "bilinear filtering" bilinear
		guibar
		guicheckbox "trilinear filtering (mipmaps)" trilinear
	]

    guitab   "res"
    guilist [
        guilist [
            guibutton "320x240"              "screenres 320 240"
            guibutton "640x480"              "screenres 640 480"
            guibutton "800x600"              "screenres 800 600"
            guibutton "1024x768"             "screenres 1024 768"
            guibutton "1152x768"             "screenres 1152 768"
            guibutton "1200x960"             "screenres 1200 960"
        ]
		guibar
        guilist [
            guibutton "1280x800"             "screenres 1280 800"
            guibutton "1280x1024"            "screenres 1280 1024"
            guibutton "1440x900"             "screenres 1440 900"
            guibutton "1600x1200"            "screenres 1600 1200"
            guibutton "1680x1050"            "screenres 1680 1050"
            guibutton "1920x1200"            "screenres 1920 1200"
        ]
    ]

    guitab   "sound"
    guitext "sound volume"
    guislider soundvol
    guitext "music volume"
    guislider musicvol
    guitext "sound channels"
    guislider soundchans
    guitext "sound frequency"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length"
    guislider soundbufferlen

    guitab    "postfx"
    guibutton "(effect OFF)"          "setfullscreenshader"
    guibutton "bloom (subtle: 80)"    "setfullscreenshader bloom 80"
    guibutton "bloom (bright: 140)"    "setfullscreenshader bloom 140"
    guibutton "bloom (intense: 200)"  "setfullscreenshader bloom 200"
    guibutton "sobel"  "setfullscreenshader sobel"
    guibutton "invert" "setfullscreenshader invert"
    guibutton "gbr"    "setfullscreenshader gbr"
    guibutton "bw"     "setfullscreenshader bw"

    guitab  "mouse"
	guicheckbox "invert mouse"      invmouse
	if (= $mousetype 0) [
		guitext "mouse yaw sensitivity"
		guislider yawsensitivity 1 (min (* 100 $sensitivityscale) 10000)
		guitext "mouse pitch sensitivity"
		guislider pitchsensitivity 1 (min (* 100 $sensitivityscale) 10000)
	] [
		guitext "mouse sensitivity"
		guislider mousesensitivity 1 100
		guitext "mouse dead zone"
		guislider mousedeadzone 1 100
		guitext "mouse pan speed"
		guislider mousepanspeed 1 1000
	]
	guitext "mouse type"
	guiradio "grabbed" mousetype 0
	guiradio "panned, relative" mousetype 1
	guiradio "panned, absolute" mousetype 2
	guiradio "free, relative" mousetype 3
	guiradio "free, absolute" mousetype 4

    guitab  "keys"
    guitext "(basic keybinds, for anything"
    guitext "more use the 'bind' command)"
    guitext "select action to bind:"
    guilistsplit n 2 $actions [
      guibutton $n (concat menubind $n)
    ]
]

actions = [forward backward left right jump crouch lean attack zoom reload "saycommand " "universaldelta 1" "universaldelta -1" edittoggle "weapon 1" "weapon 2" "weapon 3" "weapon 4" "weapon 5" ]

menubind = [ tobind = $arg1; showgui binditto ]
bindit = [ bind $arg1 $tobind ]

keys = "MOUSE1 MOUSE2 MOUSE3 MOUSE4 MOUSE5 SPACE LCTRL RCTRL ALT TAB ESCAPE BACKQUOTE SHIFT BACKSPACE ENTER INSERT DELETE HOME END PAGEUP PAGEDOWN A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 1 2 3 4 5 6 7 8 9 0 F1 F2 F3 F4 F5 F6 F7 F8 F9 F10 F11 F12"

newgui binditto [
    guilistsplit n 4 $keys [
      guibutton $n (concat bindit $n) 
    ]
]

entupdate = [ entset $tmpt $tmp0 $tmp1 $tmp2 $tmp3 ]

initentgui = [
  tmpt = ( et )
  tmp0 = ( ea 0 )
  tmp1 = ( ea 1 )
  tmp2 = ( ea 2 )
  tmp3 = ( ea 3 )
]
    
genentattributes = [
    entattributes = ""
    n = ( listlen $arg2 )
    loop i $n [       
        do [
          t@i = [ guitext @(at $arg2 $i) ]
          a@i = [ guislider tmp@i @(at $arg3 (* 2 $i)) @(at $arg3 (+ 1 (* 2 $i))) entupdate ]
          entattributes = [ @@entattributes @t@@i; @a@@i; ]
        ]
    ]
]

guilistsplit = [
  guilist [
    i = 0
    n = (listlen $arg3)
    z = (div (+ $n (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $n)
        while [< $i $t] [
          push $arg1 (at $arg3 $i)
          @@@arg4
          pop $arg1
          i = (+ $i 1)
        ]
      ]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton quicklight          "calclight -1"
  guibutton "optimize map"      "remip"
  guibutton "new entity"        "newent light"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
  guibutton "air"                        "editmat air"
  guibutton "water"                      "editmat water"
  guibutton "clip"                       "editmat clip"
  guibutton "glass"                      "editmat glass"
  guibutton "noclip"                     "editmat noclip"
  guibutton "lava"                       "editmat lava"
  guibutton "aiclip"                     "editmat aiclip"
  guibutton "death"                      "editmat death"
  guibar
  guicheckbox "show material volumes"    showmat
]

brushmenu = [
  guilist [ // diamonds
  guiimage "data/brush_1c" brush_0
  guiimage "data/brush_21c" brush_1
  guiimage "data/brush_421c" brush_2
  ]
  guilist [ // squares
  guiimage "data/brush_3s" brush_3
  guiimage "data/brush_5s" brush_4
  guiimage "data/brush_7s" brush_5
  ]
  guititle smooth
  guilist [ // smooth
  guiimage "data/brush_3s" brush_6
  guiimage "data/brush_5s" brush_7
  guiimage "data/brush_7s" brush_8
  ]
]

newentgui = [
  do [
    genentattributes [@@arg1] [@@arg2] [@@arg3]
    newgui @arg1 [
      guitext $tmpt
      guibar
      @entattributes
      guitab type 
      guilistsplit n 2 $enttypelist [
          guibutton $n [ entset @n ]       
      ]  
      guitab misc
      @quickeditmenu
    ]
  ]
]

loop i $enttypelength [
  newentgui (at $enttypelist $i) "" ""
]

newgui materials [
  @matmenu
  guitab misc
  @quickeditmenu
]

newgui brushes [
  @brushmenu
  guitab misc
  @quickeditmenu
]

newgui quickedit [
  @quickeditmenu
  guitab materials
  @matmenu
]

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui monster "direction type" "0 360 0 7"
newentgui mapmodel "direction model type trigger" "0 360 0 100 0 29 0 20"
newentgui envmap "radius" "0 400"
newentgui pusher "Z Y X" "0 200 0 200 0 200"
newentgui carrot "tag type" "0 50 0 29"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 3"

contexteditgui = [ 
  if ($hmapedit) [
    showgui brushes
  ] [
    if (enthavesel) [
      initentgui; 
      do [ showgui (et) ] 
    ] [
      if (havesel) [
        showgui materials
      ] [
        showgui quickedit
      ]
    ]
  ]
]

showentgui = [ do [ contexteditgui ] ] // legacy bind

guilistloop = [
	i = 0
	j = (+ (div $arg3 (* $arg1 $arg2)) 1)
	loop a $j [
		if (> $a 0) [ guitab $a ]
		guilist [
			loop b $arg1 [
				guilist [
					loop c $arg2 [
						if (< $i $arg3) [
							@@arg4; i = (+ $i 1)
							if (< $c (- $arg2 1)) [ guibar ]
						]
					]
				]
				if (&& (< $i $arg3) (< $b (- $arg1 1))) [ guibar ]
			]
		]
	]
]

alias showmapmodel [
	mapmodelpath = (mapmodelindex $arg1)
	mapmodelshot = (concatword "models/" $mapmodelpath "/thumb")
	mapmodelcmd = (concat newent mapmodel $arg1)
	guilist [ guilist [
		guiimage $mapmodelshot $mapmodelcmd 2 1 "textures/nothumb"
		guibutton $mapmodelpath $mapmodelcmd
	] ]
]
newgui mapmodels [
	mapmodelnum = (mapmodelindex)
	guilistloop 4 2 $mapmodelnum [ showmapmodel $i ]
]
