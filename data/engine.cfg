defaultmodifier = 0
modifier = $defaultmodifier
domodifier = [ modifier = $arg1; onrelease [ modifier = $defaultmodifier ] ]
modscale = 1

universaldelta = [
  do [delta_@(if $editing [ result edit ] [ result game ])_@modifier (* $modscale @arg1)]
]

listcomplete editmat "air water clip glass noclip lava"
air = [ editmat air ]
water = [ editmat water ]
clip = [ editmat clip ]
glass = [ editmat glass ]
noclip = [ editmat noclip ]
lava = [ editmat lava ]
death = [ editmat death ]
ladder = [ editmat ladder ]

//////// Entity Editing ///////////////

// the entity type of current selection
et = [ at (entget) 0 ]

// access the given attribute of selected ent
ea = [ at (entget) (+ $arg1 1) ]

// clear ents of given type
clearents = [ 
  if $editing [
    entcancel
    entselect [ strcmp (et) $arg1 ]; 
    echo Deleted (enthavesel) $arg1 entities; 
    delent
  ] 
]

// replace all ents that match current selection
// with the values given
replaceents = [
  if $editing [
    entfind @(entget)
    entset @arg1 @arg2 @arg3 @arg4 @arg5 @arg6
    echo Replaced (enthavesel) entities  
  ]
]

selentedit      = [ "saycommand" ( concatword "/entset " (entget) ) ] 
selreplaceents  = [ "saycommand" ( concatword "/replaceents " (entget) ) ]  
selentfindall   = [ do [ @( concatword "entfind " (entget) ) ] ]

// modify given attribute of ent by a given amount
// arg1 attribute
// arg2 value
entproperty = [
  entloop [
    a0 = (ea 0)
    a1 = (ea 1)
    a2 = (ea 2)
    a3 = (ea 3)
    a4 = (ea 4)
    do [a@arg1 = (+ (ea @arg1) @arg2)]
    entset (et) $a0 $a1 $a2 $a3 $a4
  ]
]

listindex = [
  s = 0
  n = (listlen $arg1)
  loop i $n [
    if (strcmp $arg2 (at $arg1 $i)) [
      s = $i
    ]
  ]
  result $s
]

//////// Copy and Paste //////////////

// 3 types of copying and pasting
// 1. select only cubes      -> paste only cubes
// 2. select cubes and ents  -> paste cubes and ents. same relative positions
// 3. select only ents       -> paste last selected ent. if ents are selected, replace attrs as paste

opaquepaste = 1
entcopybuf = ""

entreplace   = [ 
  do [
    if (enthavesel) [] [ newent @entcopybuf ]
    entset @entcopybuf 
  ]
]

editcopy   = [
  if (|| havesel [! (enthavesel)]) [
    entcopybuf = ""
    entcopy
    copy
  ] [
    entcopybuf = (entget)
  ]
]

editpaste  = [ 
  cancelpaste = (! (|| enthavesel havesel));
  if (strcmp "" $entcopybuf) [
    pastehilight
    onrelease [ 
      if (opaquepaste) delcube 
      paste
      entpaste
      if ($cancelpaste) [ cancelsel ] 
    ] 
  ] [
    entreplace
    if ($cancelpaste) [ cancelsel ] 
  ] 
]

/////// Selection ///////////////

equaltype  = [
  if (strcmp * $arg1) [ result 1 ] [
    result (strcmp (et) $arg1)
  ]
]

equalattr  = [
  if (strcmp * $arg2) [ result 1 ] [
    result (= (ea $arg1) $arg2)
  ]
]

// select ents with given properties
// '*' is wildcard
entfind    = [ 
  if (= ($numargs) 1) [
    entselect     [ equaltype @@arg1 ]     
  ] [ if (= ($numargs) 2) [
    entselect [ && (equaltype @@arg1)         [equalattr 0 @@arg2] ]
  ] [ if (= ($numargs) 3) [
    entselect [ && (equaltype @@arg1) (&&     [equalattr 0 @@arg2] [equalattr 1 @@arg3]) ]
  ] [ if (= ($numargs) 4) [
    entselect [ && (equaltype @@arg1) (&& (&& [equalattr 0 @@arg2] [equalattr 1 @@arg3])     [equalattr 2 @@arg4]) ]
  ] [ if (= $numargs 5) [
    entselect [ && (equaltype @@arg1) (&& (&& [equalattr 0 @@arg2] [equalattr 1 @@arg3]) (&& [equalattr 2 @@arg4] [equalattr 3 @@arg5])) ]
  ] [
    entselect [ && (equaltype @@arg1) (&& (&& (&& [equalattr 0 @@arg2] [equalattr 1 @@arg3]) (&& [equalattr 2 @@arg4] [equalattr 3 @@arg5])) [equalattr 4 @@arg6]) ]
  ]]]]]
]

lse        = [
  line = ""
  count = 0
  entloop [
    line  = ( concatword $line (entget) "		" )
    count = ( + $count 1 )
    if (> $count 4) [
      echo $line
      line = ""
      count = 0
    ]
  ]
  if (> $count 0 ) [ echo $line ]
  echo (enthavesel) entities selected
]

enttoggle  = [ entmoving 1; entmoving 0; ]
entaddmove = [ entmoving 2 ]

grabbing = 0

drag       = [ dragging 1; onrelease [ dragging 0 ] ]
corners    = [ selectcorners 1; dragging 1; onrelease [ selectcorners 0; dragging 0 ] ]
entadd     = [ entaddmove; entmoving 0; ]
editmove   = [ moving 1; onrelease [ moving 0 ]; result $moving ]
entdrag    = [ entaddmove; onrelease [entmoving 0]; result $entmoving ] 

editdrag   = [ cancelsel; || (entdrag) [ drag ] ]
selcorners = [ if ($hmapedit) [ hmapselect ] [ cancelsel; || (entdrag) [ corners ] ] ]
editextend = [ || entdrag [ selextend; reorient; editmove ] ]

editmovewith    = [
  if (havesel) [
    || (editmove) [ @arg1 ]
    onrelease [ moving 0; dragging 0 ]
  ] [
    @arg1
  ]
]

editmovecorner = [ editmovewith selcorners ]
editmovedrag   = [ editmovewith editdrag ]

////// Other Editing commands /////////

editfacewentpush = [ 
  
  if (|| [havesel] [! (enthavesel)] ) [
    if ($moving) [ 
      pushsel $arg1 
    ] [ 
      entcancel
      editface $arg1 $arg2
    ] 
  ] [
      if ($entmoving) [ entpush $arg1 ] [ ent_action_@(et) ]
  ]
]

entswithdirection = ["playerstart 0" "mapmodel 1" "teledest 0" "camera 0"]

inlist = [
  r = 0
  n = ( listlen $arg2 )
  loop i $n [
    if (strcmp $arg1 (at $arg2 $i)) [
      r = 1 
    ]
  ]
  result $r
]

indict = [
  r = $arg3
  n = (listlen $arg2)
  loop i $n [
    assoc = (at $arg2 $i)
    if (strcmp $arg1 (at $assoc 0)) [
      r = (at $assoc 1) 
    ]
  ]
  result $r
]

entdirection = [
  if ( && (enthavesel) [ = (havesel) 0 ] ) [
    diridx = (indict (et) $entswithdirection -1)
    if (>= $diridx 0) [
      if (> $arg1 0) [
        entproperty $diridx @@arg2
        if (> (ea $diridx) 360) [ entproperty $diridx -360 ]          
      ] [
        entproperty $diridx -@@arg2
        if (< (ea $diridx) 0) [ entproperty $diridx 360 ]
      ]
    ]
    result 1
  ] [
    result 0
  ]
]

editdel    = [ if (= (enthavesel) 0) [ delcube ] [ delent ] ]
editflip   = [ if (= (enthavesel) 0) [ flip ] [ entflip ] ]

editrotate = [ 
  || [ entdirection $arg1 15 ] [ 
      rotate $arg1
      entrotate $arg1 
    ] 
]

editcut    = [  
  hadselection = (havesel)
  moving 1
  if ($moving) [
    copy;     entcopy
    delcube;  delent
    onrelease [ 
      moving 0
      paste
      entpaste
      if ( ! $hadselection ) [ cancelsel ]
    ]
  ]
]

passthrough = [ 
  passthroughsel $arg1; 
  if ($arg1) [
    passthroughcube_bak = $passthroughcube
    passthroughcube 1
  ] [
    passthroughcube $passthroughcube_bak
  ]
  entcancel 
  if ( setting_entediting ) [ entediting ( ! $arg1 ) ]
  
]

alias selectents [ entselect [&& (insel) (enttype $arg1)] ]

alias entchg [
  a0 = (entattr 0)
  a1 = (entattr 1)
  a2 = (entattr 2)
  a3 = (entattr 3)
  a4 = (entattr 4)
  do [ a@arg1 = @arg2 ]
  entedit (enttype) $a0 $a1 $a2 $a3 $a4
]

noautosave = 0
alias autosave [
	if (= $noautosave 0) [
		savemap
		sleep 600000 [ autosave ]
	] [	noautosave = 0 ]
]
alias stopautosave [ noautosave = 1 ]

alias togglesound [ if (> $mastervol 0) [ mastervol 0; echo "sound off" ] [ mastervol 255; echo "sound on" ] ]

alias screen [
	showhud 0
	sleep 100 [ screenshot; echo "screenshot saved"; showhud 1; ]
]

changeoutline = [
	outlinestatus = (mod (+ $arg1 $outlinestatus) (listlen $outlinecolours))
	if (> 0 $outlinestatus) [
		outlinestatus = (+ (listlen $outlinecolours) $outlinestatus ) //just making sure it'll fit
	]
	
	if (= (+ $outlinestatus 1) (listlen $outlinecolours)) [
		echo "Outline 0"
		outline 0
	] [
		echo (concatword "Outline " (+ $outlinestatus 1))
		outline (at $outlinecolours $outlinestatus)
	]
]
outlinecolours = ["0xFF0000" "0x00FF00" "255" "1" "0xFFFF00" "0xFF00FF" "0x00FFFF" "0xFFFFFF" "0x00FF7F" "0xFF7F00" "0x7F00FF" "0xFF007F" "0x007FFF" "0"]; setcomplete outlinecolours off
outlinestatus = 0; setcomplete outlinestatus off

loadsky = [
    skybox $arg1
    if (> $numargs 1) [spinsky $arg2]
    if (> $numargs 2) [yawsky $arg3]
]
mapmsg = [maptitle $arg1]
cloudalpha = [cloudblend $arg1]
