// standard menu definitions

exec "data/menus.cfg"

newgui main [
	guilist [
		guilist [
			guitext "name: " player
			guitext "team: " flag_blue
		]
		guilist [
			guifield newname 15 [name $newname] [newname = (getname)]
			guifield newteam 15 [team $newteam] [newteam = (getteam)]
		]
	]
	guibar	
	if (! (strcmp (getname) "unnamed")) [
		guibutton "start.."	"showgui start" "sauer"

		if (= (connected) 1) [
			//guibutton "bots.."		"showgui bots"
			if (= $editing 1) [
				guibutton "editing.."	 "showgui editing"
			]
		]
		guibutton "options.."	 "showgui options"
	] [
		guitext "please enter your name to start" "sauer"
		guibar
	]
	guibutton "help.."		"showgui help"
	guibutton "about.."		"showgui about"
	guibutton "quit"		"quit"			"exit"
]

newgui start [
	guibutton "server browser.."	"showgui servers" "server"
	guibutton "update server list from master server" "updatefrommaster; showgui servers"
	guibar
	guibutton "local game" "connect localhost"
	guibutton "lanconnect"
	if (= (connected) 1) [
		guibar
		guibutton "vote game/map.." "showgui gamemode"
		guibutton "disconnect"
	]
]


newgui bots [
	guibutton "add one bot" [botadd 1]
	guibutton "add default bots" [botadd -1 -1]
	guibutton "remove one bot" [botdel 1]
	guibutton "remove all bots" [botdel 0]
	guibar
	guitext "bot ai rate"
	guislider botrate 1 100
	guibar
	guicheckbox "auto populate bots"	botauto
]

newgui help [
	guibutton "Multi-player help" "showgui mphelp"
	guitext "help on joining a multiplayer game"
	guibutton "Controls" "showgui conhelp"
	guitext "listing of default controls"
]

newgui mphelp [
	guitext "IMPORTANT: YOU MUST FORWARD UDP PORTS 28795 - 28796"
	guitext " "
	guitext "Joining an existing game:"
	guitext "open the console with `, and type connect server,"
	guitext "where server is the name of the server, or choose"
	guitext "update from master in the multiplayer menu and then"
	guitext "choose from a listed server."
	guitext " "
	guitext "Setting up a game:"
	guitext "Run server.bat instead of Blood_Frontier.bat"
]

newgui conhelp [
	guitext "DEFAULT CONTROLS:"
	guitext "W,a,s,d for movement and strafing"
	guitext "Spacebar to jump."
	guitext "Aim with your mouse."
	guitext "The left mouse button shoots."
	guitext "The right mouse button zooms the scope on the sniper"
	guitext "rifle."
	guitext "The middle mouse button reloads and picks up weapons"
	guitext "in capture mode."
]

newgui about [
	guitext "Blood Frontier by Anthony Cord"
	guitext "Based on the Sauerbraten Engine"
	guitext "http://sourceforge.net/projects/bloodfrontier/"
	guitext "Special thanks to Quinton Reeves"
	guitext "See the readme for all attributions/contributors"
	guitext " "
	guitext "Sauerbraten engine"
	guitext "by Wouter van Oortmerssen"
	guitext "with contributions from Eihrul, Gilt and others"
	guitext "(for a full list of contributors see the readme)"
	guitext "http://sauerbraten.org/"
]

maprotation = [
	n = (listlen $arg1)
	loop i $n [
	 curmap = (at $arg1 $i)
	 next = (mod (+ $i 1) $n)
	 nextmap = (at $arg1 $next)
	 (if (= $numargs 2) [concatword nextmap_ $arg2 $curmap] [concatword nextmap_ $curmap]) = $nextmap
	]
]

genmapitems = [
	n = (listlen $arg1)
	loop i $n [
	 curmap = (at $arg1 $i)
	 aliasname = (concatword bestscore_ $curmap)
	 title = (if (= (getalias $aliasname) 0) [ result $curmap ] [ concatword $curmap " (best score: " (getalias $aliasname) ")" ])
	 guibutton $title (concat map $curmap) "sauer"
	]
]

mainmaps = "usm10 usm09 usm01 usm02 usm03 usm06 usm07"
capturemaps = "usm10 usm09 usm02 usm03 usm06 usm07"
spmaps = "uss01"

maprotation $mainmaps
maprotation $capturemaps
maprotation $spmaps

alias showmapshot [
	guibar;
	mapshot = (concatword "packages/base/" (at $guirollovername 0) ".jpg")
	if (! (exists $mapshot "r")) [ mapshot = "packages/textures/notexture.png" ]
	guiimage $mapshot $guirolloveraction 4 1
]

newgui maps [
	 guilist [
	 guilist [ genmapitems $mainmaps ]
	 showmapshot
	]
]

newgui capturemaps [
	genmapitems $capturemaps
]

newgui spmaps [
	guilist [
	 guilist [ genmapitems $spmaps ]
	 showmapshot
	]
]

newgui singleplayer [
	guitext "coming soon.."
]

newgui gamemode [
	guibutton "ffa/default"	 "mode 0;	showgui maps"
	guibutton "coop edit"		"mode 1;	showgui maps"
	guibutton "ffa/duel"		"mode 2;	showgui maps"
	guibutton "teamplay"		"mode 3;	showgui maps"
	guibutton "instagib"		"mode 4;	showgui maps"
	guibutton "instagib team"	"mode 5;	showgui maps"
	guibutton "efficiency"		"mode 6;	showgui maps"
	guibutton "efficiency team"	"mode 7;	showgui maps"
	guibutton "insta arena"	 "mode 8;	showgui maps"
	guibutton "insta clan arena"	"mode 9;	showgui maps"
	guibutton "tactics arena"	"mode 10; showgui maps"
	guibutton "tactics clan arena" "mode 11; showgui maps"
	guibutton "capture"		"mode 12; showgui maps"
	guibutton "insta capture"	"mode 13; showgui maps"
]

newgui options [
	guitext "(basic keybinds, for anything"
	guitext "more use the 'bind' command)"
	guitext "select action to bind:"
	guilistsplit 2 $actions [
		guibutton $n (concat menubind $n)
	]

	guitab	"shaders"
	guicheckbox "shaders"		shaders
	guitext "shader detail (default: 3)"
	guislider shaderdetail
	guicheckbox "glass reflection"	glassenv
	guicheckbox "water refraction"	waterrefract
	guicheckbox "water reflection"	waterreflect
	guicheckbox "caustics"		caustics
	guicheckbox "grass"		grass
	guicheckbox "dynamic lights"	maxdynlights 5 0
	guicheckbox "dynamic shadows"	dynshadow 60 0
	guicheckbox "model lighting"	lightmodels
	guicheckbox "model reflection"	envmapmodels
	guicheckbox "model glow"	 glowmodels
	guicheckbox "water vertex animation" vertwater

	guitab	"display"
	guicheckbox "v-sync"		vsync 1 0
	guicheckbox "fullscreen"	 fullscreen
	guitext "gamma (default: 100)"
	guislider gamma
	guitext "fullscreen anti-aliasing (default: 0)"
	guilistslider fsaa "0 2 4 8 16"
	guitext "color depth (default: 0)"
	guilistslider colorbits "0 8 16 24 32"
	guitext "z-buffer depth (default: 0)"
	guilistslider depthbits "0 8 16 24 32"
	guitext "stencil bits (default: 1)"
	guislider stencilbits
	guicheckbox "hardware mipmaps"	hwmipmap

	guitab	"res"
	guilist [
	 guilist [
		guibutton "320x240"		"screenres 320 240"
		guibutton "640x480"		"screenres 640 480"
		guibutton "800x600"		"screenres 800 600"
		guibutton "1024x768"		"screenres 1024 768"
		guibutton "1152x768"		"screenres 1152 768"
		guibutton "1200x960"		"screenres 1200 960"
	 ]
	 guilist [
		guibutton "1280x800"		"screenres 1280 800"
		guibutton "1280x1024"		"screenres 1280 1024"
		guibutton "1440x900"		"screenres 1440 900"
		guibutton "1600x1200"		"screenres 1600 1200"
		guibutton "1680x1050"		"screenres 1680 1050"
		guibutton "1920x1200"		"screenres 1920 1200"
	 ]
]

	guitab	"sound"
	guitext "sound volume"
	guislider soundvol
	guitext "music volume"
	guislider musicvol
	guitext "sound channels"
	guislider soundchans
	guitext "sound frequency"
	guilistslider soundfreq "11025 22050 44100"
	guitext "sound buffer length"
	guislider soundbufferlen

	guitab	"fx"
	guibutton "(effect OFF)"		"setfullscreenshader"
	guibutton "bloom (subtle: 80)"	"setfullscreenshader bloom 80"
	guibutton "bloom (bright: 140)"	"setfullscreenshader bloom 140"
	guibutton "bloom (intense: 200)"	"setfullscreenshader bloom 200"
	guibutton "sobel"	"setfullscreenshader sobel"
	guibutton "invert" "setfullscreenshader invert"
	guibutton "gbr"	"setfullscreenshader gbr"
	guibutton "bw"	"setfullscreenshader bw"

	guitab	"misc"
	guicheckbox "invert mouse"	invmouse
	guicheckbox "blood"		blood
	guicheckbox "always use team skins" teamskins
	guicheckbox "outline meters"	outlinemeters
	guicheckbox "particle text"	particletext
	guicheckbox "crosshair effects" crosshairfx
	guicheckbox "hudgun sway"	hudgunsway
	guibutton "Restore Defaults" "exec data/defaults.cfg"
]

actions = [forward backward left right jump attack "saycommand	" "universaldelta 1" "universaldelta -1" edittoggle "weapon 1" "weapon 2" "weapon 3" "weapon 4" "reload" "snipe"]

menubind = [ tobind = $arg1; showgui binditto ]
bindit = [ bind $arg1 $tobind ]

keys = "MOUSE1 MOUSE2 MOUSE3 MOUSE4 MOUSE5 SPACE LCTRL RCTRL ALT TAB ESCAPE BACKQUOTE SHIFT BACKSPACE ENTER INSERT DELETE HOME END PAGEUP PAGEDOWN A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 1 2 3 4 5 6 7 8 9 0 F1 F2 F3 F4 F5 F6 F7 F8 F9 F10 F11 F12"

newgui binditto [
	guilistsplit 4 $keys [
		guibutton $n (concat bindit $n)
	]
]

guilistloop = [
	i = 0
	j = (+ (div $arg3 (* $arg1 $arg2)) 1)
	loop a $j [
		if (> $a 0) [ guitab $a ]
		guilist [
			loop b $arg1 [
				guilist [
					loop c $arg2 [
						if (< $i $arg3) [
							@@arg4; i = (+ $i 1)
							if (< $c (- $arg2 1)) [ guibar ]
						]
					]
				]
				if (&& (< $i $arg3) (< $b (- $arg1 1))) [ guibar ]
			]
		]
	]
]

alias showmapmodel [
	mapmodelpath = (getmapmodel $arg1)
	mapmodelshot = (concatword "packages/models/" $mapmodelpath "/thumb.jpg")
	mapmodelcmd = (concat newent mapmodel $arg1)
	if (exists $mapmodelshot "r") [ mapmodelshot = "packages/textures/notexture.png" ]
	guilist [ guilist [
		guiimage $mapmodelshot $mapmodelcmd 2 1
		guibutton $mapmodelpath $mapmodelcmd
	] ]
]
newgui mapmodels [
	mapmodelnum = (getmapmodel)
	guilistloop 4 2 $mapmodelnum [ showmapmodel $i ]
]
